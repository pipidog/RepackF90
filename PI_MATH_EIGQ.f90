! EIGENVALUES =================================================================  
! **** PURPOSE ****
! THIS CODE DIAGONALIZE A SYMMETRY/HERMITIAN MATRIX USING QR ALGORITHM IN LAPACK
! (SSYEV, CHEEV)
! **** INPUT VARIABLES ****
! [A]: REAL, NXN, THE MATRIX TO BE DIAGONALIZED
! **** OUTPUT VARIABLES ****
! [EVAL]: REAL, N, THE EIGENVALUES 
! **** VERSION ****
! 2/2/2014 FIRST BUILT 
! 2/4/2014 MAKE A-MATRIX AN INOUT TYPE
! **** COMMENT **** 
! 1. NOTICE: 
! THIS CODE IS JUST AN INTERFACE TO HELP USE TWO LAPACK FUNCTIONS. 
! 2. MEMORY 
! TO SAVE MEMROY,THE INUPT MATIRX WILL BE REPLACED TO EIGENVECTOR 
! MATRIX. IF YOU NEED TO KEEP YOUR ORIGINAL ONE, PLEASE SAVE IT 
! BEFORE ENTERING THE SUBROUTINE.
SUBROUTINE PI_EIGQ_SYMAT(A,A_DIM,EVAL,EVAL_DIM)
	IMPLICIT NONE
	! DIMENSION
	INTEGER, INTENT(IN) :: A_DIM(2), EVAL_DIM
	! INPUT
	REAL, INTENT(INOUT) :: A(A_DIM(1),A_DIM(2))
	! OUTPUT
	REAL, INTENT(OUT) :: EVAL(EVAL_DIM)
	! LOCAL (VALUE)
	INTEGER :: INFO, LWORK
	! LOCAL (ARRAY)
	REAL, ALLOCATABLE :: WORK(:)
	
	
	! VARIABLE INITIALIZATION ------------------------------------------------- 
	LWORK=-1
	EVAL=0
	
	! SEARCHING OPTIMAL VALUE FOR LWORK ---------------------------------------
	ALLOCATE(WORK(MAX(1,LWORK)))
    CALL SSYEV('V','L',A_DIM(1),A,A_DIM(1),EVAL,WORK,LWORK,INFO)
	LWORK=NINT(WORK(1))
	LWORK=LWORK+1
	DEALLOCATE(WORK)
    ALLOCATE(WORK(MAX(1,LWORK)))
	
	! DIAGONALIZATION
	CALL SSYEV('V','L',A_DIM(1),A,A_DIM(1),EVAL,WORK,LWORK,INFO)
	IF (INFO/=0) THEN
		WRITE(*,*) "ERROR IN PI_EIGQ_RMAT! SSYEV GET INFO/=0!"
		RETURN
	END IF
END SUBROUTINE

! CMAT ========================================================================
! **** INPUT VARIABLES ****
! [A]: COMPLEX, NXN, THE MATRIX TO BE DIAGONALIZED
! **** OUTPUT VARIABLES ****
! [EVAL]: REAL, N, THE EIGENVALUES 

SUBROUTINE PI_EIGQ_HEMAT(A,A_DIM,EVAL,EVAL_DIM)
	IMPLICIT NONE
	! DIMENSION
	INTEGER, INTENT(IN) :: A_DIM(2), EVAL_DIM
	! INPUT
	COMPLEX, INTENT(INOUT) :: A(A_DIM(1),A_DIM(2))
	! OUTPUT
	REAL, INTENT(OUT) :: EVAL(EVAL_DIM)
	! LOCAL (VALUE)
	INTEGER :: INFO, LWORK
	REAL :: RWORK(3*A_DIM(1)-2)
	! LOCAL (ARRAY)
	COMPLEX, ALLOCATABLE :: WORK(:)
	
	
	! VARIABLE INITIALIZATION ------------------------------------------------- 
	LWORK=-1
	EVAL=0
	
	! SEARCHING OPTIMAL VALUE FOR LWORK ---------------------------------------
	ALLOCATE(WORK(MAX(1,LWORK)))
    CALL CHEEV('V','L',A_DIM(1),A,A_DIM(1),EVAL,WORK,LWORK,RWORK,INFO)
	LWORK=NINT(REAL(WORK(1)))
	LWORK=LWORK+1
	DEALLOCATE(WORK)
    ALLOCATE(WORK(LWORK))
	
	! DIAGONALIZATION
	CALL CHEEV('V','L',A_DIM(1),A,A_DIM(1),EVAL,WORK,LWORK,RWORK,INFO)
	IF (INFO/=0) THEN
		WRITE(*,*) "ERROR IN PI_EIGQ_CMAT! CHEEV GET INFO/=0!"
		RETURN
	END IF
END SUBROUTINE